@ <stdio.hdr>
@ "lexer.hdr"
@ "parser.hdr"
@ "gen.hdr"
@ "string.hdr"

fn parseTerm(): -> Expr;
fn parseFactor(): -> Expr;

fn exprKindFromToken(token: Token): ExprKind
{
    switch (token.kind) {
	case ASTERISK: return EXPR_MUL;
	case SLASH: return EXPR_DIV;
	case PLUS: return EXPR_ADD;
	case MINUS: return EXPR_SUB;
	default: 
	    assert(0 && "internal error: exprKindFromToken");
	    return 0;
    }
}

fn parseExpr(): -> Expr
{
    local left: -> Expr = parseTerm();
    if (left == nullptr){
	return nullptr;
    }
    while (token.kind == PLUS || token.kind == MINUS) {
	local tok: Token = token;
        getToken();
        local right: -> Expr = parseTerm();
        if (right == nullptr) {
            printf("%u.%u expected term\n",
                   token.pos.row, token.pos.col);
            assert(0);
        }
	local exprKind: ExprKind = exprKindFromToken(tok);
	left = createBinaryExpr(exprKind, left, right);

    }
    return left;
}

fn parseTerm(): -> Expr
{
    local left: -> Expr = parseFactor();
    if (left == nullptr) {
        return nullptr;
    }
    while (token.kind == ASTERISK || token.kind == SLASH) {
	local tok: Token = token;
        getToken();
        local right: -> Expr = parseFactor();
        if (right == nullptr) {
            printf("%u.%u expected term\n",
                   token.pos.row, token.pos.col);
            assert(0);
        }
	local exprKind: ExprKind = exprKindFromToken(tok);
	left = createBinaryExpr(exprKind, left, right);
    }
    return left;
}

fn parseFactor(): -> Expr
{
    if (token.kind == DECIMAL_LITERAL) {
	local expr: -> Expr = createIntegerExpr(token.val);
        getToken();
        return expr;
    } else if (token.kind == LPAREN) {
        getToken();
        local expr: -> Expr = parseExpr();
        if (expr == nullptr) {
            printf("%u.%u expected expression\n",
                   token.pos.row, token.pos.col);
            assert(0);
        }
        if (token.kind != RPAREN) {
            printf("%u.%u expected ')'\n",
                   token.pos.row, token.pos.col);
            assert(0);
        }
        getToken();
        return expr;
    } else {
        return nullptr;
    }
}

